// quick-lint-js finds bugs in JavaScript programs.
// Copyright (C) 2020  Matthew Glazar
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <https://www.gnu.org/licenses/>.

// This file is a template for the 010 Editor. Use it to inspect GNU MO
// (translation) files generated by GNU gettext's msgfmt command.

struct Header {
    uint revision;
    uint string_count;
    uint original_strings_offset;
    uint translated_strings_offset;
    uint hash_table_size;
    uint hash_table_offset;
};

struct StringHeader {
    uint length;
    uint offset;
};

struct String(uint length) {
    char characters[length];
};

LittleEndian();
uint magic;
if (magic == 0xde120495) {
    BigEndian();
}
Header header;

local uint i;

FSeek(header.original_strings_offset);
StringHeader original_string_headers[header.string_count];

FSeek(header.translated_strings_offset);
StringHeader translated_string_headers[header.string_count];

FSeek(header.hash_table_offset);
uint hash_table[header.hash_table_size];

for (i = 0; i < header.string_count; ++i) {
    FSeek(original_string_headers[i].offset);
    String original_string(original_string_headers[i].length) <open=true>;
    FSeek(translated_string_headers[i].offset);
    String translated_string(translated_string_headers[i].length) <open=true>;
}

